buildscript {

    repositories {
        mavenLocal()
        maven { url "https://repo1.maven.org/maven2" }
    }

    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:$openAPIGeneratorVersion"
    }
}

apply plugin: "org.openapi.generator"

def bandApiClientOutput = "$buildDir/generated/openapi-code-client".toString()
def bandApiServerOutput = "$buildDir/generated/openapi-code-server".toString()

task generateBandApiClient(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {

    generatorName = "java"
    inputSpec = "src/main/resources/openapi/bands-api.yaml".toString()
    outputDir = bandApiClientOutput

    apiPackage = "org.openapi.band.client.api"
    modelPackage = "org.openapi.band.client.model"

    configOptions = [
        "dateLibrary": "java8",
        "library": "retrofit2"
    ]
}

task generateBandApiServer(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {

    generatorName = "spring"
    inputSpec = "src/main/resources/openapi/bands-api.yaml".toString()
    outputDir = bandApiServerOutput

    apiPackage = "org.openapi.band.server.api"
    modelPackage = "org.openapi.band.server.model"

    configOptions = [
        "dateLibrary": "java8",
        "hideGenerationTimestamp": "true",
        "interfaceOnly": "true",
        "java8": "true",
        "performBeanValidation": "false",
        "serializableModel": "true",
        "useBeanValidation": "true",
        "useOptional": "true"
    ]
}

compileKotlin.dependsOn(
    generateBandApiClient,
    generateBandApiServer
)

sourceSets.main.java.srcDir "$bandApiClientOutput/src/main/java"
sourceSets.main.java.srcDir "$bandApiServerOutput/src/main/java"
